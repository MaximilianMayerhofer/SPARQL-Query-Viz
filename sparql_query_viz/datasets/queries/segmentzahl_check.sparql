PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <http://example.org/onto-example.owl#>

SELECT DISTINCT ?giessanlage ?segmentzahlspec ?segmentcount
WHERE {
    ?giessanlage a/rdfs:subClassOf* :gieszanlage ; 
        :has_param ?segmentzahlparam .
    ?segmentzahlparam a :segmentzahl ;
        :int_value ?segmentzahlspec . 
    {
        SELECT ?giessanlage (COUNT(distinct ?segment) as ?segmentcount)
        WHERE {
            ?giessanlage :part ?segment . 
            ?segment a :segment . 
        }
        GROUP BY ?giessanlage
    }
    FILTER(?segmentzahlspec != ?segmentcount)
}
